# Maintainer: Tim Stahlhut <stahta01@gmail.com>

_realname=xroar
pkgbase=mingw-w64-${_realname}
pkgname=(${MINGW_PACKAGE_PREFIX}-${_realname})
pkgver=1.9
pkgrel=1
_sourcedir=${_realname}-${pkgver}
pkgdesc="Color Computer 1/2/3 emulator (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=("GPL3")
url="https://www.6809.org.uk/xroar"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" # no idea if this is required at run-time
         "${MINGW_PACKAGE_PREFIX}-SDL2"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libsystre"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-libsndfile")
makedepends=(
  'make'
  "${MINGW_PACKAGE_PREFIX}-cc"
)
checkdepends=()
options=('!strip')
source=("https://www.6809.org.uk/xroar/dl/xroar-${pkgver}.tar.gz")
sha256sums=('d98ddf3b985ca4742adcf18896549dc42210739a6933d38838a12a543db2c25e')

prepare() {
  cd "${srcdir}/${_sourcedir}"

  #patch -p1 -i ${srcdir}/
}

build() {
  cd "${srcdir}/${_sourcedir}"

  # Add missing mingw library of regex
  sed --in-place 's|MINGW_LIBS="-lmingw32 -lwinmm -mwindows -lws2_32 -lcomctl32 -lole32 -luuid"|MINGW_LIBS="-lmingw32 -lwinmm -mwindows -lws2_32 -lcomctl32 -luuid -lole32 -lregex"|' configure

  ac_cv_func_malloc_0_nonnull=yes ac_cv_func_realloc_0_nonnull=yes \
  ./configure --enable-coco3 -verbose \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --with-libpng \
    --without-gtk3 --without-gtk2 --without-gtkgl \
    --enable-filereq-cli --without-alsa \
    --without-oss --without-pulse --without-joydev
  make clean
  make -j1
}

# The ONLY ROM location that worked for me (under MINGW32) was 
# C:\Users\[USERNAME]\AppData\Local\XRoar\roms AKA %APPDATA%\..\Local\XRoar\roms

package() {
  cd "${srcdir}/${_sourcedir}"

  mkdir -p "$pkgdir${MINGW_PREFIX}/bin/"
  cp src/xroar.exe "$pkgdir${MINGW_PREFIX}/bin/"

  install -Dm644 -t "$pkgdir${MINGW_PREFIX}/share/licenses/${_realname}" COPYING.GPL
  install -Dm644 -t "$pkgdir${MINGW_PREFIX}/share/doc/${_realname}" ChangeLog
  install -Dm644 -t "$pkgdir${MINGW_PREFIX}/share/doc/${_realname}" README.SDS
  install -Dm644 -t "$pkgdir${MINGW_PREFIX}/share/doc/${_realname}" README
  cd doc
  install -Dm644 -t "$pkgdir${MINGW_PREFIX}/share/doc/${_realname}" xroar-example.conf
  #install -Dm644 -t "$pkgdir${MINGW_PREFIX}/share/doc/${_realname}" xroar.pdf
}
