# Maintainer: Tim S <stahta01@gmail.com>
# Original Author: Paul Hentschel <paul at hpminc dot com>

_realname=toolshed
_machine=m6809
_target=${_machine}-unknown

pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.2
pkgrel=2
_revision='27485e1ea881574331a76d121a045dc00acbf350'
pkgdesc="Utilities for Tandy Color Computer and Dragon microcomputers cross-development (mingw-w64)"
arch=('any')
url="http://toolshed.sourceforge.net"
license=('PerlArtistic')
groups=("${MINGW_PACKAGE_PREFIX}-${_target}-toolchain")
# depends=('glibc')
makedepends=('make' 'mercurial' 'patch' ${MINGW_PACKAGE_PREFIX}-discount)
source=("${_realname}::hg+http://hg.code.sf.net/p/toolshed/code#revision=$_revision"
  "001-ts-2.2-fix-makefile.patch"
  "002-ts-2.2-add-mingw_time-h.patch")
sha256sums=('SKIP'
            'cd70313a0c2f5aafd36eb3e5ddfc0ef87a25d4b886df3b4f06578e7d03b1e153'
            '2d42fec4ca013299bcb30834e677bac387528429f6e14bb61447a6d5ae1b8f37')

prepare() {
  cd "${_realname}"

  [[ -f "include/mingw_time.h" ]] && rm include/mingw_time.h

  patch --forward -p1 -i ${srcdir}/001-ts-2.2-fix-makefile.patch
  patch --forward -p1 -i ${srcdir}/002-ts-2.2-add-mingw_time-h.patch
}

build() {
  cd "${_realname}"
  make -C build/unix clean
  make -C build/unix
}

check() {
  cd "${_realname}"
  tests/hybrid-dsk.sh
  tests/multihdb-dsk.sh
}

package() {
  cd "${_realname}"
  make DESTDIR="$pkgdir${MINGW_PREFIX}/" -C build/unix install

  # Install license file
  sed -n '/Copyright/,/PARTICULAR PURPOSE./p' casm/src/util.h > LICENSE
  install -Dm644 -t "$pkgdir${MINGW_PREFIX}/share/licenses/${_realname}" LICENSE

  # Install image for HTML manual
  install -m644 doc/cover.jpg "$pkgdir${MINGW_PREFIX}/share/doc/${_realname}/"
}

# vim:set ts=2 sw=2 et:
