# Maintainer: Tim S <stahta01@gmail.com>

_basename=newlib
_machine_suffix=6809
_realname_suffix=lw
_realname=${_basename}${_machine_suffix}${_realname_suffix}
_filename=${_basename}
_machine=m6809
_target=${_machine}-unknown

pkgbase=newlib1.15_6809lw
pkgname=("${_target}-${_realname}")
pkgver=1.15.0
# OLD INFO NEEDS RETESTED:: Versions 1.18, 1.19, and 1.20 wants to use libgloss/m68k instead of libgloss/m6809
# OLD INFO NEEDS RETESTED:: Disable building of target-libgloss in Versions 1.18 and 1.19
# OLD INFO NEEDS RETESTED:: Version 1.20 still using libgloss/m68k instead of libgloss/m6809
# Could not get msys2 mingw32 to build versions 1.15 and 1.17 of newlib.
# Could not get msys2 mingw64 to build any version of newlib
pkgrel=1
pkgdesc="A C standard library implementation for the ${_machine} microprocessor"
arch=('i686' 'x86_64')
url="https://sourceware.org/newlib/"
license=('Various')
groups=("${_target}-toolchain")
conflicts=(${_target}-${_basename}${_machine_suffix})
provides=(${_target}-${_basename}${_machine_suffix})
depends=()
makedepends=('lwtools' 'boot-gcc6809lw' 'gcc')
options=('staticlibs' '!strip' '!emptydirs')
# OLD INFO NEEDS RETESTED:: Could not get msys2-mingw32 to build versions 1.15 and 1.17 of newlib.
# OLD INFO NEEDS RETESTED:: newlib version 1.18/19/20 gives configure.ac:26: error: Please use exactly Autoconf 2.64 instead of 2.59.
source=("ftp://sourceware.org/pub/newlib/newlib-${pkgver}.tar.gz"
        301-newlib-1.15-df-changed-seek-offset-from-int-to-_fpos_t.patch
        305-newlib-1.15-edit-config-sub.patch
        306-newlib-1.15-edit-newlib-configure-host.patch
        310-newlib-1.15-bcd-modified-source-files.patch
        311-newlib-1.15-bcd-added-source-files.patch
        312-newlib-1.15-bcd-added-.m4-files.patch
        313-newlib-1.15-bcd-added-.am-files.patch
        314-newlib-1.15-bcd-added-Makefile.in-files.patch
        315-newlib-1.15-bcd-added-configure.in-files.patch
        316-newlib-1.15-bcd-added-configure-files.patch
        317-newlib-1.15-df-modified-source-files.patch
        318-newlib-1.15-df-added-source-files.patch
        319-newlib-1.15-df-modified-build-files-except-for-configure-files.patch
        324-newlib-1.15-Add-m6809-to-configure.in.patch
        325-newlib-1.15-Add-coco-and-m6809sim-to-newlib-libc-machine-configu.patch
        326-newlib-1.15-Add-m6809-to-libgloss-configure.in.patch
        327-newlib-1.15-Add-m6809-to-newlib-libc-machine-configure.in.patch
        328-newlib-1.15-Regenerate-configure.patch
        329-newlib-1.15-Regenerate-libgloss-configure.patch
        330-newlib-1.15-Regenerate-newlib-libc-machine-configure.patch
        331-newlib-1.15-Regenerate-newlib-libc-sys-configure.patch)
sha256sums=('c4496102d38c59d1a47ddd5481af35caa1f65b76e2a94d9607737e17fd9e4465'
            '8c0f1fbcc985631a7b6efc37ff9c12837fc61531797c7189dc27986c70b18a00'
            '0daeca7d26418ea46ba7eb68af0aa78ffb47642af809e7834f86623ba8dd84f6'
            'ec140344e41fb1ea12ce79dcbcd6b70b93e0a3c198a34ec56eed052238735ec4'
            '6fb87c7acb8ac797b8f43559fbcbbbe7956028367e5a0e35a21e3b39db9b18a2'
            '0db2d8148d5fd8eb0f2e4a5d5a031ae9b06670203e0c54519496c45391ed37a7'
            '28c7cc1d2586526f0d860d931777cf0f2158ac8e4953c49701d2e8bd5fb32bcf'
            '88e6b76fb8e4c294f8d69e6e8612ebd7c3f6e7368ef38b11cd7d092ba3dcda61'
            'a6cf30515ad1ac20cadd9ad096b13f723c0be25bea6abc6d3adb8e369986862d'
            '9917500976cde31371e0525e6093f0fe57d4c4e68d46da2816b3779849ce343b'
            'f85c57e8b235ac447acb14c578f99e63d8bca4c698dc80454083e608d60b3ba4'
            'e890ff111058dc255dd82568102a9c3bcb335923e88ea6d8469b5962e8d04cf4'
            '38e848e035511a5a6a46534bdcba49e5169c2f9091661d6477760fc5af4f17f0'
            'cf7a9eaa546440bd8cf73d612240b3d129661102ea43de9bd37466be8309525b'
            '9e520d47387c360dacfe4eb88bd6f874cba85d3b9f568154f7f7976fe2683cba'
            'def9d7fed162bc0f13b504db847096b50792b5a4ec040d0bd791b86dbbd708eb'
            '21f2cc27fa79711d73391bb9c7b9cddfb314bf2392c8624d9951b25dca78ecd1'
            'f966c9291215b054a5450c2cc316615e9a4349f10ad68c83499d317cf6de9cf9'
            '281a3e570ed393e06d6e8128c73435dd5b444ca47627f7c27f9aa9fc20fb02e3'
            '1363f72d3fc52713dc439a039612ef660e1c900f04bc94e1b566958a82c224c8'
            '0d496d70737e72f1a14b526d913da7f8a5ea242ff4f54eab715cd21ac9e95542'
            'c2d3c22570e1d9d36229bbcc1f40ac8fdf1e8d071d93838fce659010dace2c16')
prepare() {
  cd ${srcdir}/${_filename}-${pkgver}

  # Changes that should work with non m6809 targets
  patch --forward -p1 -i "$srcdir"/301-newlib-1.15-df-changed-seek-offset-from-int-to-_fpos_t.patch

  # m6809 changes
  patch --forward -p1 -i "$srcdir"/305-newlib-1.15-edit-config-sub.patch
  patch -p1 -i "$srcdir"/306-newlib-1.15-edit-newlib-configure-host.patch

  patch -p1 -i "$srcdir"/310-newlib-1.15-bcd-modified-source-files.patch
  patch -p1 -i "$srcdir"/311-newlib-1.15-bcd-added-source-files.patch
  patch -p1 -i "$srcdir"/312-newlib-1.15-bcd-added-.m4-files.patch
  patch -p1 -i "$srcdir"/313-newlib-1.15-bcd-added-.am-files.patch
  patch -p1 -i "$srcdir"/314-newlib-1.15-bcd-added-Makefile.in-files.patch
  patch -p1 -i "$srcdir"/315-newlib-1.15-bcd-added-configure.in-files.patch
  patch -p1 -i "$srcdir"/316-newlib-1.15-bcd-added-configure-files.patch
  patch -p1 -i "$srcdir"/317-newlib-1.15-df-modified-source-files.patch
  patch -p1 -i "$srcdir"/318-newlib-1.15-df-added-source-files.patch
  patch -p1 -i "$srcdir"/319-newlib-1.15-df-modified-build-files-except-for-configure-files.patch
  patch -p1 -i "$srcdir"/324-newlib-1.15-Add-m6809-to-configure.in.patch
  patch -p1 -i "$srcdir"/325-newlib-1.15-Add-coco-and-m6809sim-to-newlib-libc-machine-configu.patch
  patch -p1 -i "$srcdir"/326-newlib-1.15-Add-m6809-to-libgloss-configure.in.patch
  patch -p1 -i "$srcdir"/327-newlib-1.15-Add-m6809-to-newlib-libc-machine-configure.in.patch
  patch -p1 -i "$srcdir"/328-newlib-1.15-Regenerate-configure.patch

  patch -p1 -i "$srcdir"/329-newlib-1.15-Regenerate-libgloss-configure.patch
  patch -p1 -i "$srcdir"/330-newlib-1.15-Regenerate-newlib-libc-machine-configure.patch
  patch -p1 -i "$srcdir"/331-newlib-1.15-Regenerate-newlib-libc-sys-configure.patch

  #cd ${srcdir}/${_filename}-${pkgver}
  #autoreconf-2.13 --verbose

  #cd ${srcdir}/${_filename}-${pkgver}/libgloss
  #aclocal -I ..
  #autoreconf-2.59

  #cd ${srcdir}/${_filename}-${pkgver}/newlib/libc/sys
  #aclocal -I ../../..
  #autoreconf-2.59
  ## automake --add-missing --force-missing
  #cd ${srcdir}/${_filename}-${pkgver}/newlib/libc/machine
  #aclocal -I ../../..
  #autoreconf-2.59
  ## automake --add-missing --force-missing
}

build() {
  # Build newlib
  if check_option "debug" "y"; then
    NEWLIB_CFLAGS="-O0"
  else
    # -O3 has a history of bugs in Win32 GCC; instead, use -O2
    NEWLIB_CFLAGS="-O2"
  fi
  NEWLIB_CFLAGS+=" -g -ffunction-sections -fdata-sections"
  cd $srcdir
  rm -rf build-${CARCH}
  mkdir -p $srcdir/build-${CARCH}
  cd $srcdir/build-${CARCH}

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"

  gcc --version
  m6809-unknown-gcc --version

  CCAS="${MSYSTEM_PREFIX}/bin/${_target}-gcc" \
  CC_FOR_TARGET="${_target}-gcc" \
  AS_FOR_TARGET=${MSYSTEM_PREFIX}/bin/lwasm.exe \
  AR_FOR_TARGET="${_target}-ar" \
  LD_FOR_TARGET=${MSYSTEM_PREFIX}/bin/lwlink.exe \
  AR_FLAGS=r \
  RANLIB_FOR_TARGET="/bin/true" \
  CFLAGS="${NEWLIB_CFLAGS}" \
  ../${_filename}-$pkgver/configure \
    --build=${CYGWIN_CHOST} \
    --host=${CYGWIN_CHOST} \
    --prefix=${MSYSTEM_PREFIX} \
    --target=${_target} \
    --program-prefix="${_target}-" \
    --disable-float \
    --disable-newlib-io-float \
    PATH="${MSYSTEM_PREFIX}/lib/lwtools:${MSYSTEM_PREFIX}/${_target}/bin:${MSYSTEM_PREFIX}/bin:${PATH}"
  make -j1 PATH="${MSYSTEM_PREFIX}/lib/lwtools:${MSYSTEM_PREFIX}/${_target}/bin:${MSYSTEM_PREFIX}/bin:${PATH}"
}

package() {
  cd ${srcdir}/build-${CARCH}
  make -j1 DESTDIR=${pkgdir} install

  # Remove files that conflict with host
  rm -rf "${pkgdir}${MSYSTEM_PREFIX}/share/info"
  rm -rf "${pkgdir}${MSYSTEM_PREFIX}/info"
}
