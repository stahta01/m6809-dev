# Maintainer: Tim S <stahta01@gmail.com>

_realname=vbcc
_cpu1=6809
_cpu2=6502

pkgbase=mingw-w64-${_realname}
pkgname=(
  "${MINGW_PACKAGE_PREFIX}-${_realname}${_cpu1}"
  "${MINGW_PACKAGE_PREFIX}-${_realname}${_cpu2}"
  "${MINGW_PACKAGE_PREFIX}-${_realname}"
)
pkgver=0_9h
_patch=P2
pkgrel=5
pkgdesc="code-generator for 6809/6803/68hc12 (mingw-w64)"
arch=('any')
url="http://sun.hasenbraten.de/vbcc/"
license=('custom')
groups=()
options=('!strip')
makedepends=('patch' 'make' "${MINGW_PACKAGE_PREFIX}-cc")
depends=("${MINGW_PACKAGE_PREFIX}-vlink" "${MINGW_PACKAGE_PREFIX}-vasm${_cpu1}")
source=(
  "http://phoenix.owl.de/tags/vbcc${pkgver}${_patch}.tar.gz"
  '001-Add-hc12-dt.-c-h.patch'
  '002-mingw-build-fix.patch'
  '003-Add-6502-dt.-c-h.patch'
  '004-mingw-build-fix.patch'
)
sha256sums=('1f07497c76125eee1f40414779fb9c9aa547cc2a413b73d5b5adb31f9146bcbb'
            '19c2e232b482672a816bdf95fc1e7d8b3095272fb6d1ebea4538b0d4546b324a'
            '1b1f73475bb9eab31f75f8f83300816c0aeccfdf39adf002e417e764d172b0a5'
            '276e0ded19a651399be0d21b51f7bcc90ee77559bff23e57c2a991314d460955'
            'a361226c6d3b1caf37891b92ee72c185cf65a26312a0f265fb9482e6405c0d59')

prepare() {
  cd "${_realname}"

  rm -f objects/hc12/dt.c
  rm -f objects/hc12/dt.h
  rm -f objects/6502/dt.c
  rm -f objects/6502/dt.h

  patch --forward -p1 -i ${srcdir}/001-Add-hc12-dt.-c-h.patch
  patch --forward -p1 -i ${srcdir}/002-mingw-build-fix.patch
  patch --forward -p1 -i ${srcdir}/003-Add-6502-dt.-c-h.patch
  patch --forward -p1 -i ${srcdir}/004-mingw-build-fix.patch

  rm -f machines/hc12/*.o
  rm -f machines/6502/*.o
  rm -f bin/*.exe
  touch bin/dtgen.exe
}

build() {
  cd "${_realname}"

  make TARGET=hc12

  make TARGET=6502
}

package_vbcc() {
  cd "${_realname}"

  mkdir -p "$pkgdir${MINGW_PREFIX}/bin/"
  cp ./bin/v*.exe "$pkgdir${MINGW_PREFIX}/bin/"
  rm -f $pkgdir${MINGW_PREFIX}/bin/*hc12*.exe
  rm -f $pkgdir${MINGW_PREFIX}/bin/*6502*.exe
}

package_vbcc6809() {
  pkgdesc="code-generator for 6809/6803/68hc12 (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}")

  cd "${_realname}"

  mkdir -p "$pkgdir${MINGW_PREFIX}/bin/"
  cp ./bin/*hc12*.exe "$pkgdir${MINGW_PREFIX}/bin/"
}

package_vbcc6502() {
  pkgdesc="code-generator for 6502 (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}")

  cd "${_realname}"

  mkdir -p "$pkgdir${MINGW_PREFIX}/bin/"
  cp ./bin/*6502*.exe "$pkgdir${MINGW_PREFIX}/bin/"
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
