# Maintainer: Tim S <stahta01@gmail.com>

_realname=vbcc
_cpu=6809

pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}${_cpu}")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0_9h
_patch=P2
pkgrel=3
pkgdesc="code-generator for 6809/6803/68hc12 (mingw-w64)"
arch=('any')
url="http://sun.hasenbraten.de/vbcc/"
license=('custom')
groups=()
options=('!strip')
makedepends=('patch' 'make' "${MINGW_PACKAGE_PREFIX}-cc")
depends=("${MINGW_PACKAGE_PREFIX}-vlink" "${MINGW_PACKAGE_PREFIX}-${_realname}${_cpu}")
source=(
  "http://phoenix.owl.de/tags/vbcc${pkgver}${_patch}.tar.gz"
  '001-Add-hc12-dt.-c-h.patch'
  '002-mingw-build-fix.patch'
)
sha256sums=('1f07497c76125eee1f40414779fb9c9aa547cc2a413b73d5b5adb31f9146bcbb'
            '19c2e232b482672a816bdf95fc1e7d8b3095272fb6d1ebea4538b0d4546b324a'
            '1b1f73475bb9eab31f75f8f83300816c0aeccfdf39adf002e417e764d172b0a5')

prepare() {
  cd "${_realname}"

  rm -f objects/hc12/dt.c
  rm -f objects/hc12/dt.h

  patch --forward -p1 -i ${srcdir}/001-Add-hc12-dt.-c-h.patch
  patch --forward -p1 -i ${srcdir}/002-mingw-build-fix.patch

  rm -f machines/hc12/*.o
  rm -f bin/*.exe
  touch bin/dtgen.exe
}

build() {
  cd "${_realname}"

  make TARGET=hc12
}

package_vbcc() {
  cd "${_realname}"

  mkdir -p "$pkgdir${MINGW_PREFIX}/bin/"
  cp ./bin/v*.exe "$pkgdir${MINGW_PREFIX}/bin/"
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
