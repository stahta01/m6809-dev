# Maintainer: Tim Stahlhut <stahta01@gmail.com>

_realname=vasm
_cpu1=6809
_cpu2=6502
_syntax1=oldstyle
_syntax2=std
_syntax3=mot

pkgbase=${_realname}
pkgname=(
  "${_realname}"
  "${_realname}${_cpu1}"
  "${_realname}${_cpu2}"
)
pkgver=2_0
pkgrel=2
pkgdesc="Assembler for microprocessors (mingw-w64)"
arch=('i686' 'x86_64')
url="http://sun.hasenbraten.de/vasm/"
license=('custom')
groups=()
options=('!strip')
makedepends=('patch' 'make' 'gcc')
source=(
  "http://phoenix.owl.de/tags/vasm${pkgver}.tar.gz"
  '001-fix-for-6809-LBEQ-out-of-range-error.patch'
  'LICENSE'
)
sha256sums=('210334b0326d54dfd21fe6c065ceca990a6b5da762d59c6c0f9d8c3e5f6a6093'
            'b9676fb57241c9e55c3405720ff167b57dca76470852cef3597e7e8389458e9d'
            '63b3e08e9a45ce5284263fb2a36cf6880c910b789ceda42781eb298ce8175dca')

prepare() {
  cd "${_realname}"

  patch -p1 -i ${srcdir}/001-fix-for-6809-LBEQ-out-of-range-error.patch
}

build() {
  cd "${_realname}"

  make clean
  rm -f obj/*.o
  make CPU=${_cpu1} SYNTAX=${_syntax1}
  rm -f obj/*.o
  make CPU=${_cpu1} SYNTAX=${_syntax2}
  rm -f obj/*.o
  make CPU=${_cpu1} SYNTAX=${_syntax3}
  rm -f obj/*.o
  make CPU=${_cpu2} SYNTAX=${_syntax1}
  rm -f obj/*.o
  make CPU=${_cpu2} SYNTAX=${_syntax2}
  rm -f obj/*.o
  make CPU=${_cpu2} SYNTAX=${_syntax3}
}

package_vasm() {
  cd "${_realname}"

  mkdir -p "$pkgdir${MSYSTEM_PREFIX}/bin/"
  cp vobjdump.exe "$pkgdir${MSYSTEM_PREFIX}/bin/"

  mkdir -p "$pkgdir${MSYSTEM_PREFIX}/share/licenses/${_realname}"
  install -Dm644 ${srcdir}/LICENSE \
    "$pkgdir${MSYSTEM_PREFIX}/share/licenses/${_realname}/"
}

package_vasm6809() {
  pkgdesc="Assembler for the Motorola 6809 and Hitachi 6309 microprocessors (mingw-w64)"
  depends=("${_realname}")

  cd "${_realname}"

  mkdir -p "$pkgdir${MSYSTEM_PREFIX}/bin/"
  cp vasm6809_oldstyle.exe "$pkgdir${MSYSTEM_PREFIX}/bin/"
  cp vasm6809_std.exe "$pkgdir${MSYSTEM_PREFIX}/bin/"
  cp vasm6809_mot.exe "$pkgdir${MSYSTEM_PREFIX}/bin/"
}

package_vasm6502() {
  pkgdesc="Assembler for the 6502 and related microprocessors (mingw-w64)"
  depends=("${_realname}")

  cd "${_realname}"

  mkdir -p "$pkgdir${MSYSTEM_PREFIX}/bin/"
  cp vasm6502_oldstyle.exe "$pkgdir${MSYSTEM_PREFIX}/bin/"
  cp vasm6502_std.exe "$pkgdir${MSYSTEM_PREFIX}/bin/"
  cp vasm6502_mot.exe "$pkgdir${MSYSTEM_PREFIX}/bin/"
}

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
