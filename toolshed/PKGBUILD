# Maintainer: Tim S <stahta01@gmail.com>
# Original Author: Paul Hentschel <paul at hpminc dot com>

_realname=toolshed
_machine=m6809
_target=${_machine}-unknown
_basepkgver=2.2

pkgname=${_realname}
conflicts=(${_realname}-hg)
pkgver=2.2+r868.g874bcb7
pkgrel=1
pkgdesc="Utilities for Tandy Color Computer and Dragon microcomputers cross-development"
arch=('i686' 'x86_64')
url="http://toolshed.sourceforge.net"
license=('PerlArtistic')
groups=("${_target}-toolchain")
makedepends=('gcc' 'make' 'git' 'patch')
source=("${_realname}::git+https://github.com/nitros9project/toolshed.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${_realname}"
  printf "${_basepkgver}+r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_realname}"
  sed -i 's|$(DIRS) cocofuse|$(DIRS)|' build/unix/makefile
  sed -i 's|$(APPS) cocofuse|$(APPS)|' build/unix/makefile
}

build() {
  cd "${_realname}"
  make -C build/unix clean
  make -C build/unix
}

check() {
  cd "${_realname}"
  ./tests/hybrid-dsk.sh
  ./tests/multihdb-dsk.sh
}

package() {
  cd "${_realname}"
  make DESTDIR="$pkgdir/" -C build/unix install

  # Install license file
  sed -n '/Copyright/,/PARTICULAR PURPOSE./p' casm/src/util.h > LICENSE
  install -Dm644 -t "$pkgdir${MSYSTEM_PREFIX}/share/licenses/${_realname}" LICENSE

  # Install image for HTML manual
  install -m644 doc/cover.jpg "$pkgdir${MSYSTEM_PREFIX}/share/doc/${_realname}/"
}

# vim:set ts=2 sw=2 et:
