# Maintainer: Tim S <stahta01@gmail.com>
# Original Author: Paul Hentschel <paul at hpminc dot com>

_realname=toolshed-hg
_machine=m6809
_target=${_machine}-unknown

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgbase=mingw-w64-${_realname}
pkgver=r674+.cc0c69db5793+
pkgrel=1
pkgdesc="Utilities for Tandy Color Computer and Dragon microcomputers cross-development (mingw-w64)"
arch=('any')
url='http://toolshed.sourceforge.net'
license=('PerlArtistic')
groups=("${MINGW_PACKAGE_PREFIX}-${_target}-toolchain")
#depends=('glibc')
makedepends=('mercurial' ${MINGW_PACKAGE_PREFIX}-discount)
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname%-hg}")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname%-hg}")
source=("${_realname%-hg}::hg+http://hg.code.sf.net/p/toolshed/code"
  "001-ts-2.2-fix-makefile.patch"
  "002-ts-2.2-add-mingw_time-h.patch")
sha256sums=('SKIP'
            'cd70313a0c2f5aafd36eb3e5ddfc0ef87a25d4b886df3b4f06578e7d03b1e153'
            '2d42fec4ca013299bcb30834e677bac387528429f6e14bb61447a6d5ae1b8f37')
prepare() {
  cd "$srcdir/${_realname%-hg}"

  patch -p1 -i ${srcdir}/001-ts-2.2-fix-makefile.patch
  patch -p1 -i ${srcdir}/002-ts-2.2-add-mingw_time-h.patch
}

pkgver() {
  cd "$srcdir/${_realname%-hg}"
  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

build() {
  cd "$srcdir/${_realname%-hg}"
  make -C build/unix clean
  make -C build/unix
}

check() {
  cd "$srcdir/${_realname%-hg}"
  tests/hybrid-dsk.sh
  tests/multihdb-dsk.sh
}

package() {
  cd "$srcdir/${_realname%-hg}"
  make DESTDIR="$pkgdir${MINGW_PREFIX}/" -C build/unix install

  # Install license files
  sed -n '/Copyright/,/PARTICULAR PURPOSE./p' casm/src/util.h > LICENSE
  install -Dm644 -t "$pkgdir/${MINGW_PREFIX}/share/licenses/${_realname}" LICENSE

  # Install image for HTML manual
  install -m644 doc/cover.jpg "$pkgdir/${MINGW_PREFIX}/share/doc/${_realname%-hg}/"
}

# vim:set ts=2 sw=2 et:
