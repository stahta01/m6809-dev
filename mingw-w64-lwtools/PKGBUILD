# Maintainer: Tim S <stahta01@gmail.com>
# Based on work from https://github.com/hpmachining/os9-dev

_realname=lwtools
_machine=m6809
_target=${_machine}-unknown

pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.21
pkgrel=1
pkgdesc="Cross-development tools for the Motorola 6809 and Hitachi 6309 microprocessors (mingw-w64)"
arch=('any')
url="http://lwtools.projects.l-w.ca"
license=('GPL3')
groups=("${MINGW_PACKAGE_PREFIX}-${_target}-toolchain")
options=('!strip')
makedepends=('patch' 'make' 'perl' "${MINGW_PACKAGE_PREFIX}-gcc")
conflicts=("${MINGW_PACKAGE_PREFIX}-as6809")
# install=${_realname}-${CARCH}.install
source=(
  "$url/releases/${_realname}/${_realname}-$pkgver.tar.gz"
  '003-Set-debug-in-Makefile.patch'
  '101-printf-debug-added-to-insn_rel.c.patch'
  '102-printf-debug-added-to-insn_indexed.c.patch'
)
sha256sums=('f668d943ef98be3427d683f5f3903e8e2900eb1cde4a611c988cfbb6c2b39e8f'
            '7c93dbdaf9a3667ab46fa5cc2bb8fb2c0c992bdce368a0e9a1c26a2d62b26603'
            '771caed3827c7e73f67c04d80d0cfc489ae435733dab43c3cbffafd17cf825b8'
            '7b0cc245cd487c1e492fbe1a5784e684ec15a57c0ea7f9bda358966bd19a1add')

prepare() {
  cd "${_realname}-${pkgver}"

  patch -p1 -i ${srcdir}/003-Set-debug-in-Makefile.patch
  patch -p1 -i ${srcdir}/101-printf-debug-added-to-insn_rel.c.patch
  patch -p1 -i ${srcdir}/102-printf-debug-added-to-insn_indexed.c.patch
}

build() {
  cd "${_realname}-$pkgver"
  
  echo "LDFLAGS := $LDFLAGS"

  make clean
  LDFLAGS+=" -Wl,--allow-multiple-definition" \
  make V=1
}

check() {
  cd "${_realname}-$pkgver"
  make -k test || true
}

package() {
  cd "${_realname}-$pkgver"
  make PREFIX=${MINGW_PREFIX} DESTDIR="${pkgdir}" PROGSUFFIX=.exe install

  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib"

  # Install scripts and create symbolic links
  install -Dm755 -t "$pkgdir${MINGW_PREFIX}/lib/${_realname}" extra/ar extra/as extra/ld

  mkdir -p "$pkgdir${MINGW_PREFIX}/${_target}/bin"
  cp -f $pkgdir${MINGW_PREFIX}/lib/${_realname}/ar  $pkgdir${MINGW_PREFIX}/${_target}/bin/ar
  cp -f $pkgdir${MINGW_PREFIX}/lib/${_realname}/as  $pkgdir${MINGW_PREFIX}/${_target}/bin/as
  cp -f $pkgdir${MINGW_PREFIX}/lib/${_realname}/ld  $pkgdir${MINGW_PREFIX}/${_target}/bin/ld

  cp -f "$pkgdir${MINGW_PREFIX}/lib/${_realname}/ar" "$pkgdir${MINGW_PREFIX}/bin/m6809-unknown-ar"
  cp -f "$pkgdir${MINGW_PREFIX}/lib/${_realname}/as" "$pkgdir${MINGW_PREFIX}/bin/m6809-unknown-as"
  cp -f "$pkgdir${MINGW_PREFIX}/lib/${_realname}/ld" "$pkgdir${MINGW_PREFIX}/bin/m6809-unknown-ld"

  # Install documentation
  install -Dm644 -t "$pkgdir/${MINGW_PREFIX}/share/doc/${_realname}" docs/manual/manual.{html,pdf}
}

# vim:set ts=2 sw=2 et:
