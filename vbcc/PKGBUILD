# Maintainer: Tim S <stahta01@gmail.com>

_realname=vbcc
_cpu1=6809
_cpu2=6502

pkgbase=${_realname}
pkgname=(
  "${_realname}${_cpu1}"
  "${_realname}${_cpu2}"
  "${_realname}"
)
pkgver=0_9h
_patch=P2
pkgrel=6
pkgdesc="code-generator for 6809/6803/68hc12"
arch=('i686' 'x86_64')
url="http://sun.hasenbraten.de/vbcc/"
license=('custom')
groups=()
options=('!strip')
makedepends=('patch' 'make' 'gcc')
depends=("vlink")
source=(
  "http://phoenix.owl.de/tags/vbcc${pkgver}${_patch}.tar.gz"
  '001-Add-hc12-dt.-c-h.patch'
  '002-mingw-build-fix.patch'
  '003-Add-6502-dt.-c-h.patch'
  '004-mingw-build-fix.patch'
)
sha256sums=('1f07497c76125eee1f40414779fb9c9aa547cc2a413b73d5b5adb31f9146bcbb'
            'a14d3cd284fcd9ca3cd1bfbe674afa7314c10005e67857daed7ae77eea314355'
            '1b1f73475bb9eab31f75f8f83300816c0aeccfdf39adf002e417e764d172b0a5'
            '0ca33fdb5ba368f5b3d498f20b68324021241695e3efcc9bfcc292ca858fad94'
            'a361226c6d3b1caf37891b92ee72c185cf65a26312a0f265fb9482e6405c0d59')

prepare() {
  cd "${_realname}"

  # Delete files added by patch files
  rm -f objects/hc12/dt.c
  rm -f objects/hc12/dt.h
  rm -f objects/6502/dt.c
  rm -f objects/6502/dt.h

  patch --forward -p1 -i ${srcdir}/001-Add-hc12-dt.-c-h.patch
  patch --forward -p1 -i ${srcdir}/002-mingw-build-fix.patch
  patch --forward -p1 -i ${srcdir}/003-Add-6502-dt.-c-h.patch
  patch --forward -p1 -i ${srcdir}/004-mingw-build-fix.patch

  # Clean out files
  rm -f machines/hc12/*.o
  rm -f machines/6502/*.o
  rm -f bin/*.exe
  
  # Seems to go into a loop while building without this touch
  touch bin/dtgen.exe
}

build() {
  cd "${_realname}"

  make TARGET=hc12

  make TARGET=6502
}

package_vbcc() {
  cd "${_realname}"

  mkdir -p "$pkgdir${MSYSTEM_PREFIX}/bin/"
  cp ./bin/v*.exe "$pkgdir${MSYSTEM_PREFIX}/bin/"
  rm -f $pkgdir${MSYSTEM_PREFIX}/bin/*hc12*.exe
  rm -f $pkgdir${MSYSTEM_PREFIX}/bin/*6502*.exe
}

package_vbcc6809() {
  pkgdesc="code-generator for 6809/6803/68hc12"
  depends=("${_realname}")

  cd "${_realname}"

  mkdir -p "$pkgdir${MSYSTEM_PREFIX}/bin/"
  cp ./bin/*hc12*.exe "$pkgdir${MSYSTEM_PREFIX}/bin/"
}

package_vbcc6502() {
  pkgdesc="code-generator for 6502"
  depends=("${_realname}")

  cd "${_realname}"

  mkdir -p "$pkgdir${MSYSTEM_PREFIX}/bin/"
  cp ./bin/*6502*.exe "$pkgdir${MSYSTEM_PREFIX}/bin/"
}

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
