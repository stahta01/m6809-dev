# Maintainer: Tim Stahlhut <stahta01@gmail.com>

_realname=asm6809

pkgbase=${_realname}
pkgname="${_realname}"
pkgver=2.16
pkgrel=1
pkgdesc="Cross Assembler for the Motorola 6809 and Hitachi 6309 microprocessors"
arch=('i686' 'x86_64')
makedepends=('gcc')
url="https://www.6809.org.uk/asm6809/"
license=('GPL3')
groups=('m6809-toolchain')
source=("https://www.6809.org.uk/asm6809/dl/${_realname}-${pkgver}.tar.gz")
sha256sums=('6051624597d94d69a68a08e194cfe18cbdb12f829c80d92b84f641794b8b09bb')
prepare() {
  cd ${_realname}-$pkgver

  #patch --forward -p1 -i "$srcdir"/
}

build() {
  if [[ -d "build-${_realname}-${MSYSTEM}" ]]; then
    rm -rf build-${_realname}-${MSYSTEM}
  fi
  mkdir build-${_realname}-${MSYSTEM}
  cd build-${_realname}-${MSYSTEM}

  CPPFLAGS=""
  CXXFLAGS="-O2 -Wno-error=format-security"
  CFLAGS="-O2 -Wno-error=format-security"
  ../${_realname}-$pkgver/configure \
    --prefix=${MSYSTEM_PREFIX} \
    --build=${MSYSTEM_CHOST} \
    --host=${MSYSTEM_CHOST}

  make -j1
}

package() {
  cd build-${_realname}-${MSYSTEM}

  CPPFLAGS=""
  CFLAGS="-O2"
  make -j1 DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
