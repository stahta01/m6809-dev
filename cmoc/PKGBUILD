# Maintainer: Tim S <stahta01@gmail.com>
# Based on work from https://github.com/hpmachining/os9-dev

_realname=cmoc
_machine=m6809
_target=${_machine}-unknown

pkgname=${_realname}
pkgver=0.1.90
pkgrel=1
pkgdesc="6809-generating cross-compiler for a subset of the C language"
arch=('i686' 'x86_64')
url="http://perso.b2b2c.ca/~sarrazip/dev/cmoc.html"
license=('GPL3')
groups=("${_target}-toolchain")
options=('!strip')
depends=('perl' 'lwtools' 'markdown'
         'gcc' # provides cpp
        )
makedepends=('bison' 'flex')
source=("http://perso.b2b2c.ca/~sarrazip/dev/$_realname-$pkgver.tar.gz")
sha256sums=('396f3eef99292d524471d4e2f8baeaa799608ae23ffde985f156ad023112f64c')

prepare() {
  cd "$_realname-$pkgver"
}

build() {
  # Set good default FLAGS
  export LDFLAGS="-pipe"
  export CPPFLAGS=""
  export CFLAGS="-pipe"
  export CXXFLAGS="-pipe"

  cd "$_realname-$pkgver"
  ./configure --prefix=${MSYSTEM_PREFIX}
  make clean && make -j1 && make html
}

check() {
  cd "$_realname-$pkgver"
  make  -k check -j1 # || true
}

package() {
  cd "$_realname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # Install html documentation
  install -m644 doc/cmoc-{implementation,manual,vectrex}.html "$pkgdir${MSYSTEM_PREFIX}/share/doc/$_realname"
}

# vim:set ts=2 sw=2 et:
