# Maintainer: Tim Stahlhut
# archlinux Maintainer:  Antonio Rojas
# archlinux Contributor: Sergej Pupykin
# archlinux Contributor: robb_force
# archlinux Contributor: JJDaNiMoTh
# Based on https://docs.mamedev.org/initialsetup/compilingmame.html#using-a-standard-msys2-installation
#          https://archlinux.org/packages/community/x86_64/mame/
#          https://aur.archlinux.org/packages/mame-git

pkgbase=mingw-w64-mame-tools
pkgname=("${MINGW_PACKAGE_PREFIX}-mame-tools")
pkgver=0.264.r499.g132626651dd
pkgrel=1
_sourcedir=mame-git
pkgdesc='Port of the popular Multiple Arcade Machine Emulator using SDL with OpenGL support tools (mingw-w64)'
url='https://mamedev.org/'
license=(GPL2)
arch=(any)
makedepends=(
  'git'
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-make"
  "${MINGW_PACKAGE_PREFIX}-cc"
  #"${MINGW_PACKAGE_PREFIX}-libc++" # likely needed by -fuse-ld=lld
  #"${MINGW_PACKAGE_PREFIX}-lld"    # needed by -fuse-ld=lld
)
source=(${_sourcedir}::'git+https://github.com/mamedev/mame.git#branch=master')
sha256sums=('SKIP')
options=(!debug !lto) # debug build fails under ArchLinux

pkgver() {
    cd ${_sourcedir}

    git describe --abbrev=11 --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^\(mame\)\([0-9]\)/\2./'
}

prepare() {
  cd ${_sourcedir}
}

build() {
  cd ${_sourcedir}

  if [[ -d "build-tools" ]]; then
    rm -rf build-tools
  fi
  if [[ -d "3rdparty/genie/bin" ]]; then
    rm -rf 3rdparty/genie/bin
  fi
  if [[ -d "3rdparty/genie/build/gmake.windows/obj" ]]; then
    rm -rf 3rdparty/genie/build/gmake.windows/obj
  fi

  echo "git clean"
  time git clean -fxd

  if [[ ${MSYSTEM} == "MINGW32" ]]; then
    export _MAME_TARGET=windows_x86
    export _MAME_MINGW64=
    export _MAME_MINGW32=$MINGW_PREFIX
  elif [[ ${MSYSTEM} == "MINGW64" || ${MSYSTEM} == "UCRT64" ]]; then
    export _MAME_TARGET=windows_x64
    export _MAME_MINGW64=$MINGW_PREFIX
    export _MAME_MINGW32=
  fi

  #LDFLAGS+=' -fuse-ld=lld'
  MINGW64=${_MAME_MINGW64} \
  MINGW32=${_MAME_MINGW32} \
  mingw32-make --jobs=1 ${_MAME_TARGET} \
    REGENIE=1 \
    EMULATOR=0 \
    VERBOSE=1 \
    NOWERROR=1 \
    OPTIMIZE=2 \
    TOOLS=1 \
    BUILDDIR=build-tools
}

package() {
  cd ${_sourcedir}
  for _i in castool chdman floptool imgtool jedutil ldresample ldverify nltool nlwav pngcmp regrep romcmp \
            split srcclean unidasm; do
    install -Dm755 $_i -t "$pkgdir"${MINGW_PREFIX}/bin
  done
  mv "$pkgdir"${MINGW_PREFIX}/bin/{,mame-}split # Fix conflicts

  install -Dm644 docs/man/*.1* -t "$pkgdir"${MINGW_PREFIX}/share/man/man1/
}
