# Maintainer: Tim S <stahta01@gmail.com>
# Based on work from https://github.com/hpmachining/os9-dev

_realname=cmoc
_machine=m6809
_target=${_machine}-unknown

pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1.82
pkgrel=1
pkgdesc="6809-generating cross-compiler for a subset of the C language (mingw-w64)"
arch=('any')
url="http://perso.b2b2c.ca/~sarrazip/dev/cmoc.html"
license=('GPL3')
groups=("${MINGW_PACKAGE_PREFIX}-${_target}-toolchain")
options=('!strip')
depends=('perl' "${MINGW_PACKAGE_PREFIX}-lwtools"
         "${MINGW_PACKAGE_PREFIX}-gcc" # provides cpp
         "${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=('flex' "${MINGW_PACKAGE_PREFIX}-discount")
source=("http://perso.b2b2c.ca/~sarrazip/dev/$_realname-$pkgver.tar.gz"
        '001-Build-fixes-for-MinGW.patch'
        '003-Remove-single-quotes-from-filenames.patch'
        '004-Remove-single-quotes-in-Parameters.cpp.patch')
sha256sums=('892ace681569e09b5aa0c1f4cfa3d2b86ea194c0360545799f0aaa091e06a61e'
            '0f0808d0048dcb9b5d278ef054373fbd1975a8a798d7ed2f4319c5a2f5ec3c05'
            '77ce38029e6eb9d86ab107e63bf8abf92277fe79abe5f8aa28bd7a6cccee8e71'
            '6ce51513f63fba66d5cef34518b67de05aa59febcd15a343921dc9bf34b24f35')

prepare() {
  cd "$_realname-$pkgver"
  patch -p1 -i "$srcdir"/001-Build-fixes-for-MinGW.patch
  patch -p1 -i "$srcdir"/003-Remove-single-quotes-from-filenames.patch
  patch -p1 -i "$srcdir"/004-Remove-single-quotes-in-Parameters.cpp.patch
}

build() {
  # Set good default FLAGS
  export LDFLAGS="-pipe"
  export CPPFLAGS=""
  export CFLAGS="-pipe"
  export CXXFLAGS="-pipe"

  cd "$_realname-$pkgver"
  ./configure --prefix=${MINGW_PREFIX}
  make clean && make -j1 && make html
}

check() {
  cd "$_realname-$pkgver"
  make  -k check -j1 || true
}

package() {
  cd "$_realname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # Install html documentation
  install -m644 doc/cmoc-{implementation,manual,vectrex}.html "$pkgdir${MINGW_PREFIX}/share/doc/$_realname"
}

# vim:set ts=2 sw=2 et:
